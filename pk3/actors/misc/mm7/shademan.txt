actor EvilEyes 10611
{
//$Category MM8BDM-Props
-SOLID
+NOGRAVITY
+CLIENTSIDEONLY
+BRIGHT
height 32
radius 16
scale 2.5
+RANDOMIZE
States
{
Spawn:
EVIL A 0
EVIL ABC 6
TNT1 A 6
TNT1 A 1 A_Jump(128,1)
TNT1 A 1 A_Jump(128,1)
TNT1 A 1 A_Jump(128,1)
TNT1 A 1 A_Jump(128,1)
TNT1 A 1 
EVIL BC 6
loop
}
}

actor GreenKnight 10612
{
//$Category MM8BDM-Props
+SOLID
+CLIENTSIDEONLY
height 48
radius 48
scale 2.5
States
{
Spawn:
SKNI A -1
loop
}
}

actor GhostsNGoblinsThing
{
+MISSILE
+NOGRAVITY
+DONTBLAST
+SHOOTABLE
+NOTAUTOAIMED
+DONTREFLECT
Health 1
Damage (255)
SelfObituary "$OB_GHOSTSNGOBLINSTHING"
+RIPPER
+FLOATBOB
speed 18
scale 2.5
height 30
radius 25
+FOILINVUL
States
{
Spawn:
GGBO A 0 nodelay A_ClearTarget
GGBO AB 5
goto Spawn+1
Death:
GGBO A 0 ACS_Execute(4,0)
DOMP IJKLMN 4
stop
}
}

actor Kyorown 10622
{
//$Category MM8BDM-Interactive Props
+SOLID
+SHOOTABLE
+NOTAUTOAIMED
+NOGRAVITY
//+ISMONSTER
//+RIPPER
//+MISSILE
Height 40
scale 2.5
radius 32
Health 9999
painchance 256
obituary "%o was Kyorowned by a Kyorown."
mass 999999999
+NOBLOOD
+NODAMAGE
damagefactor "KyorownBullet", 0.0
speed 0
+DONTBLAST
+QUICKTORETALIATE
States
{
Spawn:
KYOR A 0 A_JumpIfInventory("CutterFlag",1,"Taunt")
KYOR A 35 A_Wander
loop
Pain:
KYOR A 5 A_ChangeFlag("SHOOTABLE",0)
KYOR A 0 A_FaceTarget
KYOR A 0 A_GiveInventory("FireKyorownBullet", 1)
KYOR A 0 A_PlaySoundEx("misc/devilfire","Voice")
KYOR BC 4 
KYOR A 20
KYOR A 0 A_ChangeFlag("SHOOTABLE",1)
Goto Spawn
Taunt:
KYOR A 0 A_TakeInventory("CutterFlag", 99999)
KYOR A 0 A_FaceTarget
KYOR DEDEDEDE 5
KYOR A 0 A_TakeInventory("KilledMeStock",99)
KYOR A 0 A_ChangeFlag("SHOOTABLE",1)
KYOR A 0 A_ClearTarget
Goto Spawn
}
}

actor FireKyorownBullet : CustomInventory
{
	States
	{
	Pickup:
		TNT1 A 0 A_JumpIf(CallACS("core_checkservercvar", SVCVAR_HAZARD), "Credit")
		goto NoCredit
	Credit:
		TNT1 A 0 A_CustomMissile("KyorownBullet",32,0,0)
		stop
	NoCredit:
		TNT1 A 0 A_CustomMissile("KyorownBulletNoCredit",32,0,0)
		stop
	}
}

actor KyorownBullet
{
PROJECTILE
+BRIGHT
Damage (0)
Speed 35
scale 2.5
Height 8
Radius 8
+THRUACTORS
+HITTARGET
Damagetype "KyorownBullet"
SelfObituary "$OB_KYOROWNBULLET"
States
{
Spawn:
BASB A 0
BASB A 0 A_RearrangePointers(AAPTR_NULL, AAPTR_TARGET, AAPTR_NULL)
BASB A 0 A_TransferPointer(AAPTR_MASTER, AAPTR_DEFAULT, AAPTR_TARGET, AAPTR_TRACER) // "who are you aiming at, boss?"
BASB A 0 A_ChangeFlag("THRUACTORS", 0)
Bullet:
BASB A 1
wait
Death:
TNT1 A 0 A_TransferPointer(AAPTR_DEFAULT, AAPTR_MASTER, AAPTR_TARGET, AAPTR_TARGET) // "hey boss, i hit this guy, maybe not who you aimed at"
TNT1 A 2 ACS_NamedExecuteWithResult("core_damageowner", 25, AAPTR_TARGET, AAPTR_TRACER)
TNT1 A 0 A_JumpIfInventory("IsDead", 1, "GiveTaunt", AAPTR_TARGET)
stop
GiveTaunt:
TNT1 A 1 A_GiveInventory("CutterFlag", 1, AAPTR_MASTER)
stop
}
}

actor HazardCredit_KyorownBullet : BasicACSDamager
{
	DamageType "KyorownBullet"
	Obituary "$OB_KYOROWNCREDIT"
}

actor KyorownBulletNoCredit
{
PROJECTILE
+BRIGHT
Damage (25)
Speed 35
scale 2.5
Height 8
Radius 8
+THRUACTORS
+HITTRACER
Damagetype "KyorownBullet"
SelfObituary "$OB_KYOROWNBULLET"
States
{
Spawn:
BASB A 0
BASB A 0 A_RearrangePointers(AAPTR_NULL, AAPTR_TARGET, AAPTR_NULL)
BASB A 0 A_ChangeFlag("THRUACTORS", 0)
Bullet:
BASB A 1
wait
Death:
TNT1 A 1 A_JumpIfInventory("IsDead", 1, "GiveTaunt", AAPTR_TRACER)
stop
GiveTaunt:
TNT1 A 1 A_GiveInventory("CutterFlag", 1, AAPTR_MASTER)
stop
}
}

actor GNGSpawner 10629
{
//$Category MM8BDM-Interactive Props
//$Sprite GGBOA0
+NOGRAVITY
+NOINTERACTION
-SOLID
States
{
Spawn:
TNT1 A 1
loop
Missile:
TNT1 A 1 A_CustomMissile("GhostsNGoblinsThing",0,0,0,2,0)
Goto Spawn
}
}

actor ShadeManCandle 10643
{
//$Category MM8BDM-Props
+FORCEYBILLBOARD
+CLIENTSIDEONLY
+NOGRAVITY
+BRIGHT
Height 32
radius 16
scale 2.5
states
{
Spawn:
SHTR BCDE 4
loop
}
}