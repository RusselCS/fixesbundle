actor ConcreteSpike 11712
{
//$Category MM8BDM-Interactive Props
//$Sprite CNSPA0
+DONTBLAST
+NOGRAVITY
+THRUGHOST
Height 16
radius 16
scale 2.5
states
{
Spawn:
TNT1 A 0 
TNT1 A 1 A_SpawnItemEx("SpikeDamager") // From Spiney
goto Graphic
Graphic:
CNSP A -1
stop
}
}

actor Paozo 11706
{
//$Category MM8BDM-Props
//$Arg0 Color
//$Arg0Enum { 0 = "Green"; 1 = "Red"; }
//$Arg0Type 11
+SOLID
height 128
radius 64
scale 2.5
States
{
Spawn:
PAOZ A 0
PAOZ A 0 A_JumpIf(args[0]>0, "Flavor2")
Flavor1:
PAOZ A 1
loop
Flavor2:
PAOZ A 0 A_SpawnItemEx("RedPaozo")
stop
}
}

actor RedPaozo : Paozo
{
translation "128:128=41:41"
}

actor PaozoBall 11707
{
//$Category MM8BDM-Props
//$Arg0 Color
//$Arg0Enum { 0 = "Green"; 1 = "Red"; }
//$Arg0Type 11
//$NotAngled
+SOLID
height 64
radius 32
scale 2.5
States
{
Spawn:
PAOZ B 0
PAOZ B 0 A_JumpIf(args[0]>0, "Flavor2")
Flavor1:
PAOZ B 1
loop
Flavor2:
PAOZ A 0 A_SpawnItemEx("RedPaozoBall")
stop
}
}

actor RedPaozoBall : PaozoBall
{
translation "128:128=41:41"
}

actor Hoohoo // birb
{
PROJECTILE
+DONTBLAST
+RIPPER
+BLOODLESSIMPACT
-SOLID
+NOGRAVITY
+DONTREFLECT
//+THRUACTORS
+SHOOTABLE
+NOTAUTOAIMED
+NOBLOOD
-NOBLOCKMAP
+DONTDRAIN
+NODAMAGE
Painchance 256
Health 1
Damage (0)
radius 16
height 16
scale 2.5
speed 0
States
{
Spawn:
CNBR AA 0 A_ChangeVelocity(10, 0, 0, CVF_REPLACE|CVF_RELATIVE)
CNBR A 4 A_Weave(0, 6, 0, 1.0)
CNBR A 4 A_Weave(0, 6, 0, 1.0)
CNBR B 4 A_Weave(0, 6, 0, 1.0)
CNBR B 4 A_Weave(0, 6, 0, 1.0)
loop
Pain:
CNBR C 0 A_ChangeFlag(NOPAIN, true)
CNBR C 0 A_ClearTarget
CNBR C 0 A_SpawnItemEx("HoohooRock", 0, 0, -10, momx/2)
CNBR C 8 ThrustThingZ(0, 12, 0, 0)
CNBR D 4 A_ChangeVelocity(10, 0, momz/2, CVF_REPLACE|CVF_RELATIVE)
CNBR D 4 A_ChangeVelocity(10, 0, 0, CVF_REPLACE|CVF_RELATIVE)
Goto Spawn2
Spawn2:
CNBR EEDD 4 A_Weave(0, 6, 0, 1.5)
loop
}
}

actor HoohooRock
{
PROJECTILE
-NOGRAVITY
+DONTREFLECT
+RIPPER
Damage 0
Speed 0
radius 5
height 5
Scale 2.5
States
{
Spawn:
CNBR F 1
loop
Death:
CNBR FFFF 0 A_SpawnItemEx("HoohooShrapnel", 0, 0, 0, random(1, 5), 0, random(5, 10), random(0, 359))
stop
}
}

actor HoohooShrapnel
{
PROJECTILE
-NOGRAVITY
+RIPPER
+DONTREFLECT
Damage 0
Speed 0
radius 5
height 5
Scale 2.5
States
{
Spawn:
CNBR I 0
CNBR I 0 A_Jump(64, "Spawn1")
CNBR I 0 A_Jump(96, "Spawn2")
CNBR I 1
wait
Spawn1:
CNBR G 1
wait
Spawn2:
CNBR H 1
wait
}
}